#!/usr/bin/env fish

set -l script_dir (status dirname)
set -l basename (basename "$script_dir")

source "$script_dir/../constants.fish"

echo "Updating '$basename'"

set -l url 'https://repology.org/project/pdfstudioviewer/information'
set -l regex $VERSION_REGEX
set -l xpath "//section[@id='Versions']/ul/li/span[@title='newest version']"

echo 'Executing the `parse-html` script'
set -l latest_version ("$script_dir/../../scripts/parse-html" $url $regex $xpath)

if test -z "$latest_version"
  echo "Failed to get the latest version for '$basename'"
  echo 'Exitting...'
  exit 1
end

set -l latest_year (echo $latest_version | cut -d. -f1)
set -l latest_underscore_version (string replace --all '.' '_' $latest_version)

set -l current_version (cat "$script_dir/$basename.spec" | rg -or '$1' "Version:\\s*$VERSION_REGEX")

set -l current_year (echo $current_version | cut -d. -f1)
set -l current_underscore_version (string replace --all '.' '_' $current_version)

echo 'Comparing the current version ($current_version) with the latest version ($latest_version)'
echo "Current version: $current_version"
echo "Latest version: $latest_version"
if test "$current_version" != "$latest_version"
  echo 'Versions are different. Updating the spec file(s)...'
  for v in (fd --glob '*.spec' $script_dir)
    sd -F "year $current_year" "year $latest_year" $v
    sd -F "underscore_version $current_underscore_version" "underscore_version $latest_underscore_version" $v
    sd "(Version\\:\\s*)$current_version" "\${1}$latest_version" $v
  end && echo 'Done!'
else
  echo 'Both versions are the same. No update needed.'
end

echo "Finished updating '$basename'"
