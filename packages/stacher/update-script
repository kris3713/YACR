#!/usr/bin/env fish

set -l script_dir (status dirname)
set -l basename (basename "$script_dir")

echo "Updating '$basename'"

set -l url 'https://www.videohelp.com/software/Stacher'
set -l regex 'Stacher\\s([\\d.]+)'
set -l xpath '/html/head/title'

echo 'Executing the `parse-html` script'
set -l latest_version ("$script_dir/../../scripts/parse-html" $url $regex $xpath)

if test -z "$latest_version"
  echo "Failed to get the latest version for '$basename'"
  echo 'Exitting...'
  exit 1
end

set -l latest_major (echo $latest_version | cut -d. -f1)

set -l current_version (cat "$script_dir/$basename.spec" | rg -or '$1' 'Version:\\s*([\\d.]+)')

set -l current_major (echo $current_version | cut -d. -f1)

echo 'Comparing the current version ($current_version) with the latest version ($latest_version)'
echo "Current version: $current_version"
echo "Latest version: $latest_version"
if test "$current_version" != "$latest_version"
  echo 'Versions are different. Updating the spec file(s)...'
  for v in (fd --glob '*.spec' $script_dir)
    sd -F "major $current_major" "major $latest_major" $v
    sd "(Version\\:\\s*)$current_version" "\${1}$latest_version" $v
  end && echo 'Done!'
else
  echo 'Both versions are the same. No update needed.'
end

echo "Finished updating '$basename'"
